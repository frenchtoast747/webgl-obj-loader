<!DOCTYPE html>
<html>

<head>
  <meta charset="UTF-8">
  
  <script type="text/javascript" src="gl-matrix.js"></script>
  <script type="text/javascript" src="webgl-utils.js"></script>

  <!-- Main Shader -->

  <script id="shader-fs" type="x-shader/x-fragment">
    #version 100
    #define M_PI 3.1415926535897932384626433832795
    precision mediump float;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;

    uniform vec3 uAmbientColor;

    uniform vec4 uColor;
    uniform vec3 uLightLocation;
    uniform vec3 uSpotDirection;
    uniform vec3 uLightSpecularColor;
    uniform vec3 uLightDiffuseColor;
    uniform bool uHasTexure;
    uniform bool uHasFlashlight;

    uniform sampler2D uSampler;

    uniform float uMaterialShininess;

    struct Pointlight {
      vec3 pos;
      float intensity;
    };

    vec2 pointlight(Pointlight light, vec3 surface, vec3 normal) {
      vec3 vectorToLightSource = (light.pos - surface);
      vec3 normalToLightSource = normalize(vectorToLightSource);
      vec3 reflectionVector = normalize(reflect(vectorToLightSource, normal));
      vec3 viewVectorEye = normalize(vPosition.xyz / vPosition.w);
      float specular = max(dot(reflectionVector, viewVectorEye), 0.0);
      float shading = clamp(dot(normalToLightSource, normal), 0.0, 1.0);
      float falloff = clamp(1.0/dot(vectorToLightSource, vectorToLightSource), 0.0, 1.0);
      float diffuse = shading*falloff;
      return light.intensity * vec2(diffuse, specular);
    }

    struct Spotlight {
      vec3 pos;
      vec3 dir;
      float intensity;
      float angle;
      float fuzz_angle;
    };

    vec2 spotlight(Spotlight light, vec3 surface, vec3 normal) {
      float cutoff1 = cos(light.angle/2.0);
      float cutoff2 = cos((light.angle-light.fuzz_angle)/2.0);
      vec3 normalToLightSource = normalize(light.pos - surface);
      float insideCone = -dot(normalToLightSource, light.dir);
      return smoothstep(cutoff1, cutoff2, insideCone) * pointlight(Pointlight(light.pos, light.intensity), surface, normal);
    }

    const Spotlight flashlight = Spotlight(vec3(0.0, 0.0, 0.0),
                                           vec3(0.0, 0.0, -1.0),
                                           2.0,
                                           M_PI / 8.0,
                                           M_PI / 180.0);

    void main(void) {
      Spotlight ceilinglight = Spotlight(uLightLocation,
                                         normalize(uSpotDirection),
                                         1.0,
                                         M_PI / 4.0,
                                         M_PI / 90.0);
      Pointlight foglight = Pointlight(vec3(0.0, 0.0, 0.0),
                                       2.0);
      vec2 lights = vec2(0.0, 0.0);
      lights += spotlight(ceilinglight, vPosition.xyz, vTransformedNormal);
      lights += float(uHasFlashlight) * spotlight(flashlight, vPosition.xyz, vTransformedNormal);
      lights += pointlight(foglight, vPosition.xyz, vTransformedNormal);

      vec4 color = mix(uColor, texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t)), float(uHasTexure));
      vec3 diffuse = color.rgb * lights[0];
      vec3 specular = uLightSpecularColor * lights[1]*uMaterialShininess;
      gl_FragColor = vec4(diffuse + specular, 1.0);
    }
  </script>

  <script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
    varying vec4 vColor;


    void main(void) {
        vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        gl_Position = uPMatrix * vPosition;
        vTextureCoord = aTextureCoord;
        vTransformedNormal = uNMatrix * aVertexNormal;
    }
  </script>

  <!-- particle shader -->
  <script id="particle-shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform sampler2D uSampler;

    varying float dead;
    varying float vAlpha;

    void main(void) {
      if( dead > 0.0 ){
        discard;
      }
      float intensity = 0.01;
      vec4 color = vec4( intensity * 0.88, intensity * 0.78, intensity * 0.51, 1.0 );
      vec4 map = texture2D(uSampler, gl_PointCoord);
      float alpha = map.r * 255.0;
      if( alpha < 0.5 )
        discard;
      gl_FragColor = color * map;
      gl_FragColor.a = alpha;
    }
  </script>

  <script id="particle-shader-vs" type="x-shader/x-vertex">
    attribute vec3 aParticlePosition;
    attribute vec3 aParticleVector;
    attribute float aParticleTTL;

    varying float dead;
    varying float vAlpha;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform float time;

    void main(void) {
    if( time > aParticleTTL ){
      dead = 1.0;
      return;
    }
    else{
      dead = -1.0;
    }
      vec4 position = vec4(aParticlePosition + ( aParticleVector * time ), 1.0 );
      vec4 pos = position;
      pos = uMVMatrix * pos;
      vAlpha = pos.z / time;
      gl_PointSize = 50.0 / length( pos.xyz );
      gl_Position = uPMatrix * uMVMatrix * position;
    }
  </script>

  <script type='text/javascript' src='webgl-obj-loader.js'></script>
  <script type='text/javascript' src='globals.js'></script>
  <script type='text/javascript' src='keyframes.js'></script>
  <script type='text/javascript' src='helpers.js'></script>
  <script type='text/javascript' src='initialize.js'></script>
  <script type='text/javascript' src='camera.js'></script>
  <script type='text/javascript' src='tunnel.js'></script>
  <script type='text/javascript' src='tunnelMovement.js'></script>
  <script type='text/javascript' src='monkeyroom.js'></script>
  <script type='text/javascript' src='webgl.js'></script>
</head>

<body style='padding:0; margin:0;'>

  <div style='float:left; width:50%;'>
    <canvas id="mycanvas" style="border: none;" width="500" height="500"></canvas><br />
  </div>
  <div style='float:left; width:50%;'>
    <h2>Illinois Jones and the Tunnel Run</h2>
    You are world famous archaeologist Illinois Jones and you have stumbled upon the legendary Blender monkey, Suzanne.
    Navigate through the room using standard WASD and SHIFT controls for movement and click on the canvas to initiate mouse pointerlock. Press 'f'
    to turn on your flashlight.
    When you're done exploring the room, go grab Suzanne by pressing 'e'. But be warned: the room may not be as peaceful as it appears.
  </div>

</body>

</html>
